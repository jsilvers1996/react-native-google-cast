apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility w/react-native versions still on gradle plugin 6    
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace = "com.reactnative.googlecast"
    }
    compileSdkVersion safeExtGet('compileSdkVersion', 31)
    buildToolsVersion safeExtGet('buildToolsVersion', '31.0.0')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 31)
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    implementation "com.android.support:appcompat-v7:${safeExtGet('supportLibVersion', '+')}"
    implementation "com.android.support:mediarouter-v7:${safeExtGet('supportLibVersion', '+')}"
    implementation "com.google.android.gms:play-services-cast-framework:${safeExtGet('castFrameworkVersion', '+')}"
}
